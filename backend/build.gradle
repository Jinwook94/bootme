plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id "org.asciidoctor.jvm.convert" version "3.3.2"
    id 'java'
}

group = 'com'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    asciidoctorExt
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
    implementation 'junit:junit:4.13.1'
    implementation 'com.auth0:java-jwt:3.19.0'
    implementation 'com.auth0:jwks-rsa:0.21.1'
    implementation 'com.google.http-client:google-http-client-jackson2:1.31.0'
    implementation 'com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'io.rest-assured:rest-assured:4.5.1'
    compileOnly 'org.projectlombok:lombok'
    compileOnly 'com.google.api-client:google-api-client:1.32.1'
    runtimeOnly 'com.h2database:h2'
    runtimeOnly 'mysql:mysql-connector-java'
    runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'
    runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
    runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'
    annotationProcessor 'org.projectlombok:lombok'

    // rest docs
    asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'
    testImplementation 'org.springframework.restdocs:spring-restdocs-mockmvc'
    testImplementation 'org.springframework.restdocs:spring-restdocs-restassured'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
    useJUnitPlatform()
}

asciidoctor.doFirst {
    delete file('src/main/resources/static/docs')
}

asciidoctor {
    dependsOn test
    inputs.dir snippetsDir
    configurations 'asciidoctorExt'
    baseDirFollowsSourceFile()
}

task copyDocument(type: Copy) {
    dependsOn asciidoctor
    from file("build/docs/asciidoc")
    into file("src/main/resources/static/docs")
}

task buildDocument(type: Copy) {
    dependsOn copyDocument
    from file("src/main/resources/static/docs")
    into file("build/resources/main/static/docs")
}

bootJar {
    // 스프링 프로필이 'prod' 아니면 빌드되지 않도록 설정
    Properties properties = new Properties()
    InputStream inputStream = new FileInputStream(project.rootProject.file('src/main/resources/application.yml'))
    properties.load(inputStream)
    def activeProfile = properties.getProperty("active")

    if (activeProfile == 'prod') {
        def dateTime = new Date().format('yyyy-MMdd-HHmm')
        archivesBaseName = 'bootme-' + dateTime
        archiveFileName = archivesBaseName + '.jar'
    } else {
        throw new StopExecutionException("스프링 프로필이 'prod'로 설정되어 있지 않아 빌드를 취소했습니다. (현재 프로필: $activeProfile) \n" +
                'application.yml 파일의 "spring.profiles.active" 값을 확인하세요.')
    }
    dependsOn buildDocument
    enabled = true
}
